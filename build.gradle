plugins {
    id 'groovy'
    id 'wrapper'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

repositories {
    jcenter()
    mavenCentral()
}


dependencies {
    compile gradleApi()
    compile localGroovy()

    compileOnly(group: "com.querydsl", name: "querydsl-jpa", version: "${queryDslVersion}")
    annotationProcessor(group: "com.querydsl", name: "querydsl-apt", version: "${queryDslVersion}")

    implementation 'org.codehaus.groovy:groovy-all:2.5.4'
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}

group = 'com.kuartz.core.plugin.gradle'
version = '1.3.5'
sourceCompatibility = 1.8

gradlePlugin {
    plugins {
        mavenPublishPlugin {
            id = 'com.kuartz.core.plugin.gradle'
            implementationClass = 'com.kuartz.gradle.plugin.KuartzPlugin'
        }
    }
}

task groocvyDoc(type: Jar) {
    from groovydoc
    archiveClassifier = 'groovydoc'
}

def gprusr
def gprkey
def githubProperties = new Properties()
def file = rootProject.file("github.properties")
if (file.exists()) {
    githubProperties.load(new FileInputStream(file))
    gprusr = githubProperties["gprusr"]
    gprkey = githubProperties["gprkey"]
} else {
    gprusr = System.getenv("GITHUB_USERNAME")
    gprkey = System.getenv("GITHUB_TOKEN")
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/kutay-celebi/kuartz-core")
            credentials {
                username = gprusr
                password = gprkey
            }
        }
    }
    publications {
        publication(MavenPublication) {
            artifact groocvyDoc
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            pom {
                name = 'Kuart Gradle Plugin'
                description = 'Kuartz custom gradle plugins'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kutaycelebi'
                        name = 'Kutay Ã‡elebi'
                        email = 'mail@kutaycelebi.com'
                    }
                }
            }
        }
    }
}
